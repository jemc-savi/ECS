:class ECS.World.Spec
  :is Spec
  :const describes: "ECS.World"

  :it "can allocate an entity and retrieve the same entity by name"
    world = ECS.World.new
    foo = world.entity("Foo")

    assert: world["Foo"]!.id == foo.id
    assert error: world["Bar"]!

    assert: world.entity("Foo").id == foo.id
    assert: world.entity("Bar").id != foo.id

  :it "can signal that a quit is desired"
    world = ECS.World.new
    assert: world.should_quit == False
    assert: world.progress == True

    world.quit

    assert: world.should_quit == True
    assert: world.progress == False

  :it "can tell the total number of frames that have elapsed"
    world = ECS.World.new
    assert: world.frame_count_total == 0

    assert: world.progress
    assert: world.frame_count_total == 1

    assert: world.progress
    assert: world.frame_count_total == 2

  :it "can get and set the target FPS value"
    world = ECS.World.new
    assert: world.target_fps == 0

    world.target_fps = 60
    assert: world.target_fps == 60

    world.target_fps = 123.5
    assert: world.target_fps == 123.5
